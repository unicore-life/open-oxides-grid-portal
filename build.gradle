buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.4.2.RELEASE',
                'io.spring.gradle:dependency-management-plugin:0.6.1.RELEASE'
    }
}

plugins {
    id 'application'
    id 'java'
    id 'groovy'
    id 'checkstyle'
    id 'com.moowork.grunt' version '0.13'
    id "com.moowork.node" version "0.13"
    id 'pl.allegro.tech.build.axion-release' version '1.4.1'
}
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'


scmVersion.tag.prefix = project.name

group = 'pl.edu.icm.unicore.portal'
project.version = scmVersion.version


dependencies {
    compile 'org.springframework.boot:spring-boot-starter',
            'org.springframework.boot:spring-boot-starter-undertow',
            'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.boot:spring-boot-starter-thymeleaf',
            'org.springframework.boot:spring-boot-starter-security',

            'cglib:cglib:3.2.4',
            'org.apache.commons:commons-lang3:3.5',
            'org.springframework:spring-context-support:4.3.4.RELEASE',
            'net.sf.ehcache:ehcache:2.10.3',

            'pl.edu.icm.unicore:unicore-spring-boot-starter:0.1.5'

    testCompile 'org.springframework.boot:spring-boot-starter-test',
            'org.spockframework:spock-spring:1.0-groovy-2.4',
            'org.apache.httpcomponents:httpclient'
}

repositories {
    mavenCentral()
    maven { url 'http://unicore-dev.zam.kfa-juelich.de/maven' }
    maven { url 'http://dl.bintray.com/unicore-life/maven' }
}


sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

mainClassName = 'pl.edu.icm.oxides.OxidesGridPortal'
run {
    jvmArgs = ['-Dspring.profiles.active=local']
}

clean {
    delete "${rootDir}/bower_components"
    delete "${rootDir}/node_modules"
}

applicationDistribution.from('src/main/resources') {
    into 'config'
    include 'application-prod.yml'
    include 'grid-prod.properties'
    rename '(.+)-prod.(.+)', '$1.$2'
}

node {
    version = '6.9.1'
    download = true
}

grunt {
    workDir = file("${project.projectDir}")
    colors = false
    bufferOutput = true
}

project.tasks.grunt_install.dependsOn npmInstall
project.tasks.grunt_build.dependsOn grunt_install
project.tasks.processResources.dependsOn grunt_build

project.tasks.grunt_check.dependsOn grunt_install
project.tasks.check.dependsOn grunt_check

task wrapper(type: Wrapper) {
    gradleVersion = '3.2'
}
