buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE',
                'io.spring.gradle:dependency-management-plugin:0.5.4.RELEASE'
    }
}

plugins {
    id 'application'
    id 'java'
    id 'groovy'
    id 'checkstyle'
    id 'com.moowork.grunt' version '0.11'
}
apply plugin: 'spring-boot'
apply plugin: 'io.spring.dependency-management'

dependencies {
    compile 'org.springframework.boot:spring-boot-starter',
            'org.springframework.boot:spring-boot-starter-undertow',
            'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.boot:spring-boot-starter-thymeleaf',
            'org.springframework.boot:spring-boot-starter-security',

            'org.apache.commons:commons-lang3:3.4',
            'org.springframework:spring-context-support:4.2.4.RELEASE',
            'net.sf.ehcache:ehcache:2.10.1',

            'eu.unicore.workflow:common:2.8.1',
//            'eu.unicore:uas-client:1.9.4'
            files('securityLibrary-4.2.2.20160105.jar')

    compile('eu.unicore:uas-client:1.9.4') {
        exclude group: 'eu.unicore.security', module: 'securityLibrary'
    }

    testCompile 'org.springframework.boot:spring-boot-starter-test',
            'org.spockframework:spock-spring:1.0-groovy-2.4',
            'org.apache.httpcomponents:httpclient'
}

// FIXME: workaround until uas-client is updated to use security library with version 4.3.0+
distZip { excludes = ["**/securityLibrary-4.2.2.jar"] }

configurations {
    all*.exclude module: 'javax.servlet'
}

repositories {
    mavenCentral()
    maven { url 'http://unicore-dev.zam.kfa-juelich.de/maven' }
}

group = 'pl.edu.icm.unicore.portal'
version = '1.1.1-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

mainClassName = 'pl.edu.icm.oxides.OxidesGridPortal'
run {
    jvmArgs = ['-Dspring.profiles.active=local']
}

applicationDistribution.from('src/main/resources') {
    into 'config'
    include 'application-prod.yml'
    include 'grid-prod.properties'
    rename '(.+)-prod.(.+)', '$1.$2'
}

node {
    version = '5.1.0'
    download = true
}

grunt {
    workDir = file("${project.projectDir}")
    colors = false
    bufferOutput = true
}

project.tasks.grunt_install.dependsOn npmInstall
project.tasks.grunt_build.dependsOn grunt_install
project.tasks.processResources.dependsOn grunt_build

project.tasks.grunt_check.dependsOn grunt_install
project.tasks.check.dependsOn grunt_check

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}
